apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.yu.hu.traveling"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //开启dataBinding
    dataBinding {
        enabled true
    }

    //启用lambda表达式
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    //为了解决部分第三方库重复打包了META-INF的问题
    packagingOptions{
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }

    //apk名设置
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            //def date = new Date().format("yyyyMMdd" , TimeZone.getTimeZone("GMT+08"))
            if(variant.buildType.name.equals('debug')){
                ///"commonui_${date}_${versionName}.apk"
                outputFileName = "Traveling.apk"
            }
            if(variant.buildType.name.equals('release')){
                outputFileName = "Traveling${versionName}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    //libCommon
    implementation project(path: ':libcommon')
    //自定义注解处理器
    implementation project(path: ':libnavannotation')
    annotationProcessor project(":libnavcompiler")

    //room注解处理器
    annotationProcessor "androidx.room:room-compiler:$rootProject.roomVersion"

    //network
    implementation project(path: ':libnetwork')
    //ninegridlayout
    implementation project(path: ':ninegridlayout')
    //qq登录
    implementation files('libs/open_sdk_r6140_lite.jar')
    implementation project(path: ':emoji')
    implementation project(path: ':leancloudchatkit')

    //aliyun oss
    implementation 'com.aliyun.dpa:oss-android-sdk:+'
}
